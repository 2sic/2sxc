-- Skip update in case installation has been done by 00.99.00.SqlDataProvider (installs everything until and including 08.11.00)
IF NOT EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'[dbo].[ToSIC_SexyContent_Templates]') AND TYPE IN (N'U')) AND
EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'[dbo].[ToSIC_EAV_Apps]') AND TYPE IN (N'U'))
BEGIN
	RETURN
END

-- Because this SQL should not be validated if the condition above is met (return is called), it's wrapped in an EXEC
EXEC('
-- Script Block for 2SexyContent 05.05.00 Update
-- ---------------------------------------------
-- This SQL Script Block must be included in every .SqlDataProvider file
-- for 2SexyContent <= 05.05.00 because of the rename of the internal
-- module name. It does not have to be included for newer SQL scripts.
-- This script changes the module name and the breaks the installation
-- so the DNN cache is refreshed. After the upgrade is triggered again,
-- the installation works.

DECLARE @IsUpgradeFromPreviousVersion int
SELECT @IsUpgradeFromPreviousVersion = CASE WHEN EXISTS(SELECT * FROM DesktopModules WHERE ModuleName = N''2SexyContent'') THEN CAST (1 AS BIT) ELSE CAST (0 AS BIT) END

If @IsUpgradeFromPreviousVersion = 1
BEGIN
	UPDATE       DesktopModules
	SET                ModuleName = N''2sxc''
	WHERE        (ModuleName = N''2SexyContent'')

	RAISERROR(N''Please run this upgrade again to finish the process. This is a necessary step because of the internal rename of the 2SexyContent module. ************************************************************************************************************************************************************************** PLEASE RUN THIS UPGRADE AGAIN TO FINISH THE PROCESS. This is a necessary step because of the internal rename of the 2SexyContent module. **************************************************************************************************************************************************************************'', 16, 1)
	RETURN
END

-- End of the Script Block for 2SexyContent 05.05.00 Update

Begin Transaction

Declare @ChangeLogID int
Declare @NewAttributeSetID int
Declare @NewAttributeSetName nvarchar(100) = ''2SexyContent-Template-ContentTypes''

-- Insert ChangeLog and define ChangeLog ID
Insert Into ToSIC_EAV_ChangeLog ([Timestamp], [User])
Values (GETDATE(), ''System'')
Set @ChangeLogID = @@Identity

-- Check if it already exists, otherwise exit
Select @NewAttributeSetID = AttributeSetID 
From ToSIC_EAV_AttributeSets
Where StaticName = @NewAttributeSetName

If @NewAttributeSetID IS NULL
Begin
	
	-- Insert new record, if not already existing
	Insert Into ToSIC_EAV_AttributeSets (StaticName, Name, Scope, [Description], ChangeLogCreated)
	Values (''2SexyContent-Template-ContentTypes'', ''2SexyContent-Template-ContentTypes'', ''SexyContent-System'', ''2SexyContent-Template-Defaults'', @ChangeLogID)

	-- Get ID
	Select @NewAttributeSetID = AttributeSetID 
	From ToSIC_EAV_AttributeSets
	Where StaticName = @NewAttributeSetName

	-- Insert Attributes & AttributesInSets

	-- ItemType
	Insert Into ToSIC_EAV_Attributes (StaticName, [Type], ChangeLogCreated)
	Values (''ItemType'', ''String'', @ChangeLogID)
	Insert Into ToSIC_EAV_AttributesInSets (AttributeSetID, AttributeID, IsTitle, SortOrder, AttributeGroupID)
	Values (@NewAttributeSetID, @@IDENTITY, 1, 0, 1)

	-- ContentTypeID
	Insert Into ToSIC_EAV_Attributes (StaticName, [Type], ChangeLogCreated)
	Values (''ContentTypeID'', ''Number'', @ChangeLogID)
	Insert Into ToSIC_EAV_AttributesInSets (AttributeSetID, AttributeID, IsTitle, SortOrder, AttributeGroupID)
	Values (@NewAttributeSetID, @@IDENTITY, 1, 1, 1)

	-- DemoEntityID
	Insert Into ToSIC_EAV_Attributes (StaticName, [Type], ChangeLogCreated)
	Values (''DemoEntityID'', ''Number'', @ChangeLogID)
	Insert Into ToSIC_EAV_AttributesInSets (AttributeSetID, AttributeID, IsTitle, SortOrder, AttributeGroupID)
	Values (@NewAttributeSetID, @@IDENTITY, 1, 2, 1)

End

-- Add "Paths" field to File Control Parameters
Declare @FileAttributeSetID int

Select @FileAttributeSetID = AttributeSetID 
From ToSIC_EAV_AttributeSets
Where StaticName = ''@File''

If @FileAttributeSetID IS NOT NULL
Begin
	Declare @PathsField int

	Select @PathsField = ToSIC_EAV_Attributes.AttributeID
	From ToSIC_EAV_Attributes INNER JOIN
	ToSIC_EAV_AttributesInSets ON ToSIC_EAV_Attributes.AttributeID = ToSIC_EAV_AttributesInSets.AttributeID
	Where ToSIC_EAV_Attributes.StaticName = ''Paths'' And ToSIC_EAV_AttributesInSets.AttributeSetID = @FileAttributeSetID

	If @PathsField IS NULL
	Begin
		Insert Into ToSIC_EAV_Attributes (StaticName, [Type], ChangeLogCreated)
		Values (''Paths'', ''String'', @ChangeLogID)
		Insert Into ToSIC_EAV_AttributesInSets (AttributeSetID, AttributeID, IsTitle, SortOrder, AttributeGroupID)
		Values (@FileAttributeSetID, @@IDENTITY, 0, 1, 1)
	End
End

Commit')