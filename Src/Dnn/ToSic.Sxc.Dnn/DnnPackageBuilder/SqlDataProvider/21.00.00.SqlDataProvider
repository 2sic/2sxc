SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

-- make sure sql rolls back automatically in case of error.
SET XACT_ABORT ON
GO

-- =====================================================================
-- Ensure TsDynDataHistory exists and has ParentRef (eg. "app-42")
-- =====================================================================

-- Add ParentRef column on existing installations
IF NOT EXISTS (SELECT * FROM sys.columns WHERE Name = N'ParentRef' AND Object_ID = OBJECT_ID(N'[dbo].[TsDynDataHistory]'))
    AND OBJECT_ID('[dbo].[TsDynDataHistory]', 'U') IS NOT NULL
BEGIN
    PRINT '... Adding column [ParentRef] to [dbo].[TsDynDataHistory]';
    ALTER TABLE [dbo].[TsDynDataHistory] ADD [ParentRef] [nvarchar](250) NULL;
END

-- Add indexes for ParentRef
IF NOT EXISTS (SELECT * FROM sys.indexes WHERE name = 'IX_TsDynDataHistory_ParentRef' AND object_id = OBJECT_ID('[dbo].[TsDynDataHistory]'))
    AND OBJECT_ID('[dbo].[TsDynDataHistory]', 'U') IS NOT NULL
BEGIN
    PRINT '... Adding index IX_TsDynDataHistory_ParentRef';
    CREATE NONCLUSTERED INDEX [IX_TsDynDataHistory_ParentRef] ON [dbo].[TsDynDataHistory] ([ParentRef] ASC)
    WITH (PAD_INDEX = OFF, STATISTICS_NORECOMPUTE = OFF, SORT_IN_TEMPDB = OFF, DROP_EXISTING = OFF, ONLINE = OFF, ALLOW_ROW_LOCKS = ON, ALLOW_PAGE_LOCKS = ON) ON [PRIMARY];
END

-- =====================================================================
-- TsDynDataRelationship: add ChildExternalId to support undelete
-- =====================================================================

-- Add ChildExternalId column on existing installations
IF NOT EXISTS (SELECT * FROM sys.columns WHERE Name = N'ChildExternalId' AND Object_ID = OBJECT_ID(N'[dbo].[TsDynDataRelationship]'))
    AND OBJECT_ID('[dbo].[TsDynDataRelationship]', 'U') IS NOT NULL
BEGIN
    PRINT '... Adding column [ChildExternalId] to [dbo].[TsDynDataRelationship]';
    ALTER TABLE [dbo].[TsDynDataRelationship] ADD [ChildExternalId] [uniqueidentifier] NULL;
END

-- Remove TransDeletedId (was a short-lived experiment)
IF OBJECT_ID('[dbo].[TsDynDataRelationship]', 'U') IS NOT NULL
BEGIN
    IF EXISTS (
        SELECT *
        FROM sys.foreign_keys
        WHERE name = 'FK_TsDynDataRelationship_TsDynDataTransactionDeleted'
          AND parent_object_id = OBJECT_ID('[dbo].[TsDynDataRelationship]')
    )
    BEGIN
        PRINT '... Dropping FK FK_TsDynDataRelationship_TsDynDataTransactionDeleted';
        ALTER TABLE [dbo].[TsDynDataRelationship] DROP CONSTRAINT [FK_TsDynDataRelationship_TsDynDataTransactionDeleted];
    END

    IF EXISTS (SELECT * FROM sys.indexes WHERE name = 'IX_TsDynDataRelationship_TransDeletedId' AND object_id = OBJECT_ID('[dbo].[TsDynDataRelationship]'))
    BEGIN
        PRINT '... Dropping index IX_TsDynDataRelationship_TransDeletedId';
        DROP INDEX [IX_TsDynDataRelationship_TransDeletedId] ON [dbo].[TsDynDataRelationship];
    END

    IF EXISTS (SELECT * FROM sys.columns WHERE Name = N'TransDeletedId' AND Object_ID = OBJECT_ID(N'[dbo].[TsDynDataRelationship]'))
    BEGIN
        PRINT '... Dropping column [TransDeletedId] from [dbo].[TsDynDataRelationship]';
        ALTER TABLE [dbo].[TsDynDataRelationship] DROP COLUMN [TransDeletedId];
    END
END
GO
